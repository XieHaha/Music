apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def releaseTime() {
    return new Date().format("MM月dd日", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 18
        versionName "2.1.1"
        flavorDimensions "default"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a'
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions "zyc"
    productFlavors {
        zyc {
            dimension "zyc"
            applicationId "com.yht.yihuantong"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://www.zychealth.com\""
            resValue "string", "APP_NAME", "\"会珍医生版\""
            manifestPlaceholders = [ease_appkey : "1110180330253249#dpappproject",
                                    jpush_appkey: "8b803f7221a77f19b78b3523"]
        }

        develop {
            dimension "zyc"
            applicationId "com.yht.yihuantong"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://test.zychealth.com\""
            resValue "string", "APP_NAME", "\"会珍医生测试版\""
            manifestPlaceholders = [ease_appkey : "1110180330253249#dpappproject",
                                    jpush_appkey: "8b803f7221a77f19b78b3523"]
        }

        beta {
            dimension "zyc"
            applicationId "com.yht.yihuantong_test"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://gray.zychealth.com\""
//            buildConfigField "String", "BASE_BASIC_URL", "\"http://39.107.249.194:7080/DPView/\""
            resValue "string", "APP_NAME", "\"会珍医生灰度版\""
            manifestPlaceholders = [ease_appkey : "1105190122042534#dp-test-app",
                                    jpush_appkey: "0253e6c0183a34f72630c349"]
        }
    }
    signingConfigs {
        release {
            keyAlias '智予成'
            keyPassword '123456'
            storeFile file('F:/ZYC/上线/zyc.jks')
            storePassword 'android'
        }
        debug {
            keyAlias '测试'
            keyPassword '999999'
            storeFile file('F:/ZYC/上线/zyc.jks')
            storePassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //移除无用的resource文件
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                checkReleaseBuilds false
                abortOnError false
            }
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '26.0.0'
                }
            }
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "会珍医生版V${variant.versionName}-${releaseTime()}.apk"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':shortcutbadge')
    implementation project(':easeui')
    implementation project(':superweb')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    //kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //角标
    implementation 'cn.bingoogolapple:bga-badgeview:1.0.6@aar'
    //游标 图片
    implementation 'com.bigkoo:convenientbanner:2.0.5'
    //image loader
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    //json
    implementation 'com.alibaba:fastjson:1.2.8'
    //时间选择器
    implementation 'com.contrarywind:Android-PickerView:4.1.3'
    //二维码
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    //网络
    implementation 'com.yanzhenjie.nohttp:nohttp:1.1.10'
    //数据库
    implementation 'org.litepal.android:core:1.6.1'
    //适配界面
    implementation 'me.jessyan:autosize:1.1.2'
    //圆形图片
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    //图片管理库
    implementation 'com.zhihu.android:matisse:0.5.2-beta3'
    //注解
    implementation 'com.jakewharton:butterknife:8.8.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'

    //retrofit网络请求框架
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    //retrofit添加Json解析返回数据
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    //retrofit添加Rxjava支持
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    //rxjava框架
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
}
repositories {
    mavenCentral()
}
